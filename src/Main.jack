class Main {
    function void main ( ) {
        var Parser parser;
        var String input;
        var Plot plot;
        var int scale;
        var char key;
        var boolean firstDraw;
        var Calculator calculator;

        let key = 0;
        let plot = Plot.new(0, 6, 511, 249);
        while (~(key = 140)) {
            let input = Keyboard.readLine("");
            let calculator = Calculator.new(parser.parseToModifiedArray(input));
            let firstDraw = true;
            let scale = 20;
            let key = 0;
            while ((~(key = 139)) & (~(key = 140))) { // while (key != 'DEL' & key != 'ESC')
                let key = Keyboard.keyPressed();
                if ((key = 65) | (key = 97) | (key = 130)) { // key == 'A' | key == 'a' | key == '←'
                    do plot.moveLeft(10);
                }
                if ((key = 68) | (key = 100) | (key = 132)) { // key == 'D' | key == 'd' | key == '→'
                    do plot.moveRight(10);
                }
                if ((key = 87) | (key = 119) | (key = 131)) { // key == 'W'| key == 'w' | key == '↑'
                    do plot.moveUp(10);
                }
                if ((key = 83) | (key = 115) | (key = 133)) { // key == 'S' | key == 's' | key == '↓'
                    do plot.moveDown(10);
                }
                if (key = 43) { // key == '+'
                    let scale = scale + 10;
                }
                if ((key = 45) & (scale > 10)) { // key == '-'
                    let scale = scale - 10;
                }

                if ((key > 0) | firstDraw) {
                    do Screen.clearScreen();
                    do plot.drawBase();
                    do plot.drawFunction(scale, calculator);
                }

                if (key = 139) { // key == 'DEL' новый график
                    do calculator.dispose();
                    do input.dispose();
                    do Output.moveCursor(0, 0);
                }
                let firstDraw = false;
            }
            do Screen.clearScreen();
        }
        return;
    }
}