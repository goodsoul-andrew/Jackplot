class StringList {
    field StringListItem start;
    field StringListItem end;
    field int length;

    constructor StringList new(){
        let length = 0;
        let start = null;
        let end = null;
        return this;
    }

    method void add(String data){
        var StringListItem prev;

        if (start = null){
            let start = StringListItem.new(data, null, null, 0);
            let end = start;
        }
        else{
            let prev = end;
            let end = StringListItem.new(data, null, end, end.getIndex() + 1);
            do prev.setNext(end);
        }
        let length = length + 1;
        return;
    }

    method void removeAtLast(){
        var StringListItem last;

        if (end = null){
            do Sys.error(18);
        }
        if (end = start){
            let last = end;
            let end = null;
            let start = null;
            do last.dispose();
            return;
        }
        let last = end;
        let end = end.getPrev();
        if (~(end = null)){
            do end.setNext(null);
        }
        do last.dispose();
        let length = length - 1;
        return;
    }

    method void changeAtIndex(int index, String changed_data){
        var StringListItem current;
        var int current_index;

        let current = start;
        let current_index = 0;
        if (index > length | index = length){
            return;
        }
        while(current_index < index){
            let current = current.getNext();
            let current_index = current_index + 1;
        }
        do current.setData(changed_data);
        return;
    }

    method String takeAtIndex(int index){
        var StringListItem current;
        var int current_index;

        let current = start;
        let current_index = 0;
        if (index > length | index = length){
            do Sys.error(15);
        }
        while(current_index < index){
            let current = current.getNext();
            let current_index = current_index + 1;
        }
        return current.getData();
    }

    method void dispose(){
        var StringListItem current;

        let current = start;
        while (~(start = null)){
            let start = start.getNext();
            do current.dispose();
            let current = start;
        }
        do Memory.deAlloc(this);
        return;
    }

    method void print(){
        var StringListItem current;

        let current = start;
        while (~(current = null)){
            do Output.printString(current.getData());
            do Output.printChar(32);
            let current = current.getNext();
        }
        return;
    }

    method Array toArray(){
        var Array result;
        var StringListItem current;
        
        let result = Array.new(length);
        let current = start;
        while (~(current = null)){
            let result[current.getIndex()] = current.getData();
            let current = current.getNext();
        }
        return result;
    }

    method int length(){
        return length;
    }

    method ModifiedArray toModifiedArray() {
        var ModifiedArray result;
        var StringListItem current;
        var Array arr;

        let result = ModifiedArray.new(length);
        let current = start;
        while (~(current = null)){
            let arr = result.getData();
            let arr[current.getIndex()] = current.getData();
            let current = current.getNext();
        }
        return result;
    }
}